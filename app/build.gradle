apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion '24 rc2'

    defaultConfig {
        applicationId "com.rothanak.gradeskinda"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        test {
            java.srcDirs 'src/testLib/java'
            assets.srcDirs 'src/testLib/assets'
        }
        androidTest {
            java.srcDirs 'src/testLib/java'
            assets.srcDirs 'src/testLib/assets'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'

        // Workaround: https://code.google.com/p/android/issues/detail?id=195331
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

// Workaround to allow Wiremock to access its HTML response stubs in instrumentation tests, since
// aapt doesn't package asset folders starting with an underscore and the name of Wiremock's stubs
// directory (__files) can't be changed. A custom AssetSource in androidTest allows Wiremock to read
// assets from the mockserver directory.
// See: https://code.google.com/p/android/issues/detail?id=161356 [1]
//      https://github.com/tomakehurst/wiremock/issues/47 [2]
task copyServerFiles(type: Copy) {
    from 'src/testLib/assets/mockserver/__files'
    into new File(buildDir, 'intermediates/assets/androidTest/debug/mockserver')
}
// TODO run on ALL assembleDebug tasks: http://stackoverflow.com/a/18423209/5623874
assembleDebug.dependsOn copyServerFiles

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.2.1'

    // Java 8 lambdas
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.0.6'

    // MultiDex
    compile 'com.android.support:multidex:1.0.1'

    // Fix version conflicts
    testCompile 'com.android.support:support-annotations:23.2.1';
    androidTestCompile 'com.android.support:support-annotations:23.2.1';

    // RxJava
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'

    // View injectons and bindings
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'

    // Dependency Injection
    compile 'com.google.dagger:dagger:2.0.1'
    apt "com.google.dagger:dagger-compiler:2.0.1"
    compile 'org.glassfish:javax.annotation:10.0-b28'

    // Logging
    compile 'com.jakewharton.timber:timber:3.1.0'

    // Networking
    compile('com.squareup.retrofit:retrofit:2.0.0-beta2') {
        exclude group: 'com.squareup', module: 'okhttp'
    }
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile 'com.squareup.okhttp:okhttp:2.7.0'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.0'

    // HTML Parser
    compile 'org.jsoup:jsoup:1.8.3'

    // Instrumentation testing
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'junit', module: 'junit'
    }
    androidTestCompile('com.github.tomakehurst:wiremock:2.0.8-beta') {
        // https://github.com/tomakehurst/wiremock/issues/53#issuecomment-169204337
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'asm', module: 'asm'
        exclude group: 'org.json', module: 'json'
    }
    androidTestCompile 'org.apache.httpcomponents:httpclient-android:4.3.5'

    // Unit testing
    testCompile 'junit:junit:4.12'
    testCompile 'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1'
    testCompile 'org.assertj:assertj-core:2.2.0'
    testCompile 'com.squareup.assertj:assertj-android:1.1.0'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:1.7.5'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    testCompile('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testCompile 'com.github.tomakehurst:wiremock:2.0.8-beta'
}

configurations {
    all*.exclude group: 'com.android.support', module: 'support-annoations'
}
